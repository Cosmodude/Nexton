#pragma version ^0.4.0;

#include "../imports/stdlib.fc";

global int      msg::op;
global int      msg::query_id;
global int      msg::bounced?;

global slice    msg::sender;
global int      msg::balance;
global int      msg::value;
global int      msg::fwd_fee;

;; load the message: op, sender, contract balance, msg value, fwd fee; modifying method
;; usage msg_body~ctx::load(msg, balance, value);
(slice, ()) ctx::load(slice body, cell msg, int balance, int value) impure inline {
    slice cs = msg.begin_parse();

    int bounced? = cs~load_uint(4) & 1;
    slice sender = cs~load_msg_addr();

    cs~load_msg_addr(); ;; skip dst
    cs~load_coins();    ;; skip value
    cs~skip_bits(1);    ;; skip extracurrency collection
    cs~load_coins();    ;; skip ihr_fee

    if (bounced?) { body~skip_bits(32); }

    msg::op         = body~load_uint(32); ;; modifying methods
    msg::query_id   = body~load_uint(64);
    msg::bounced?   = bounced?;

    msg::sender     = sender;
    msg::balance    = balance;  ;; contract balance
    msg::value      = value;    ;; msg value
    msg::fwd_fee    = muldiv(cs~load_coins(), 3, 2);  ;; fwd fees are set by blockchain, see config https://explorer.toncoin.org/config?workchain=-1&shard=8000000000000000&seqno=22185244&roothash=165D55B3CFFC4043BFC43F81C1A3F2C41B69B33D6615D46FBFD2036256756382&filehash=69C43394D872B02C334B75F59464B2848CD4E23031C03CA7F3B1F98E8A13EE05

    return (body, ());
}

;; sending excess message  
() ctx::send_excesses(slice to, int flag) impure inline {
    cell body = begin_cell()
        .store_uint(op::general::excesses , 32)
        .store_uint(msg::query_id, 64)
        .end_cell();

    send_raw_message(delib::int_msg(to, false, 0, body, null()), flag);
}

() ctx::only_owner(int err, slice owner_address) impure inline {
    throw_unless(err, equal_slice_bits(msg::sender, owner_address));
}

;; reserve jettons (target or balance - msg.value)
() ctx::reserve(int target) impure inline {
    raw_reserve(max(msg::balance - msg::value, target), 0);
}